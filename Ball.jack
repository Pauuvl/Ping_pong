class Ball {
    field int x, y;          // posición actual
    field int vx, vy;        // velocidad en X e Y
    field int radius;        // tamaño de la bola
    field int bounceCount;   // contador de rebotes consecutivos

    /** Constructor: crea la bola y la inicializa */
    constructor Ball new() {
        let radius = 4;
        do reset();
        return this;
    }

    /** Reinicia la posición y velocidad de la bola */
   method void reset() {
    do erase();
    let x = 256;
    let y = 128;
    let vy = 3;
    let bounceCount = 0;

    // cambia de dirección cada vez
    if (vx > 0) {
        let vx = -4;
    } else {
        let vx = 4;
    }

    do draw();
    return;
}


    /** Actualiza la posición y gestiona colisiones */
    method void update(Paddle leftPaddle, Paddle rightPaddle) {
        var int nx, ny;
        do erase();

        // calcular nueva posición
        let nx = x + vx;
        let ny = y + vy;

        // rebote superior
        if (ny < (20 + radius)) {
            let ny = 20 + radius;
            let vy = -vy;
        }

        // rebote inferior
        if (ny > (235 - radius)) {
            let ny = 235 - radius;
            let vy = -vy;
        }

        // actualizar posición
        let x = nx;
        let y = ny;

        // colisión con paddle izquierdo
        if ((vx < 0) & (x < (leftPaddle.getX() + 8 + radius))) {
            if ((y > leftPaddle.top()) & (y < leftPaddle.bottom())) {
                let x = leftPaddle.getX() + 8 + radius;
                let vx = -vx;
                let bounceCount = bounceCount + 1;
                if (bounceCount = 3) {
                    let vx = vx + 1;
                    let bounceCount = 0;
                }
            }
        }

        // colisión con paddle derecho
        if ((vx > 0) & (x > (rightPaddle.getX() - radius))) {
            if ((y > rightPaddle.top()) & (y < rightPaddle.bottom())) {
                let x = rightPaddle.getX() - radius;
                let vx = -vx;
                let bounceCount = bounceCount + 1;
                if (bounceCount = 3) {
                    let vx = vx - 1;
                    let bounceCount = 0;
                }
            }
        }

        do draw();
        return;
    }

    /** Dibuja la bola en pantalla */
    method void draw() {
        if ((x > radius) & (x < (512 - radius)) & (y > radius) & (y < (256 - radius))) {
            do Screen.setColor(true);
            do Screen.drawCircle(x, y, radius);
        }
        return;
    }

    /** Borra la bola de la pantalla */
    method void erase() {
        if ((x > radius) & (x < (512 - radius)) & (y > radius) & (y < (256 - radius))) {
            do Screen.setColor(false);
            do Screen.drawCircle(x, y, radius);
        }
        return;
    }

    /** Detecta si se marcó gol hacia la izquierda */
    method boolean isGoalLeft() {
        return (x < (radius + 5));
    }

    /** Detecta si se marcó gol hacia la derecha */
    method boolean isGoalRight() {
        return (x > (507 - radius));
    }

    /** Libera la memoria de la bola */
     method void dispose() {
        do erase();
        do Memory.deAlloc(this);
        return;
    }

    /** Métodos de acceso para compatibilidad */
    method int getX() { return x; }
    method int getY() { return y; }
    method int getSize() { return radius; }
}

