class Paddle {
    field int x, y, width, height, speed;

    /** Constructor: inicializa la posición y dibuja el paddle */
    constructor Paddle new(int sx, int sy) {
        let x = sx;
        let y = sy;
        let width = 8;
        let height = 40;
        let speed = 7;
        do draw();
        return this;
    }

    /** Libera la memoria del paddle */
    method void dispose() {
        do erase();
        do Memory.deAlloc(this);
        return;
    }

    /** Mueve el paddle hacia arriba */
    method void moveUp() {
        do erase();
        let y = y - speed;
        if (y < 21) { let y = 21; }       // límite superior
        do draw();
        return;
    }

    /** Mueve el paddle hacia abajo */
    method void moveDown() {
        do erase();
        let y = y + speed;
        if ((y + height) > 234) { let y = 234 - height; }  // límite inferior
        do draw();
        return;
    }

    /** Dibuja el paddle en pantalla */
    method void draw() {
        if ((x > 0) & (x < 511) & (y > 0) & ((y + height) < 255)) {
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + width, y + height);
        }
        return;
    }

    /** Borra el paddle de la pantalla */
    method void erase() {
        if ((x > 0) & (x < 511) & (y > 0) & ((y + height) < 255)) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + width, y + height);
        }
        return;
    }

    /** Retorna coordenadas o dimensiones */
    method int getX() { return x; }
    method int getY() { return y; }
    method int getWidth() { return width; }
    method int getHeight() { return height; }
    method int top() { return y; }
    method int bottom() { return y + height; }

    /** Verifica colisión con la pelota */
    method boolean collidesWith(Ball ball) {
        var int bx, by, bsize;
        let bx = ball.getX();
        let by = ball.getY();
        let bsize = ball.getSize();

        if ((bx + bsize) < x) { return false; }
        if (bx > (x + width)) { return false; }
        if ((by + bsize) < y) { return false; }
        if (by > (y + height)) { return false; }

        return true;
    }
}
